/*
 * Copyright (c) 2018. Lorem ipsum dolor sit amet, consectetur adipiscing elit.
 * Morbi non lorem porttitor neque feugiat blandit. Ut vitae ipsum eget quam lacinia accumsan.
 * Etiam sed turpis ac ipsum condimentum fringilla. Maecenas magna.
 * Proin dapibus sapien vel ante. Aliquam erat volutpat. Pellentesque sagittis ligula eget metus.
 * Vestibulum commodo. Ut rhoncus gravida arcu.
 */

//#################################### Bintray ####################################//
if (project.rootProject.file('local.properties').exists()) {
    apply plugin: 'com.jfrog.bintray'

    version = "1.0"     // This is the library version used when deploying the artifact

    // Homepage URL of the library
    def siteUrl = 'https://github.com/kevalpatel2106/FingerprintDialogCompat'
    // Git repository URL
    def gitUrl = 'https://github.com/kevalpatel2106/FingerprintDialogCompat.git'

    group = "com.kevalpatel2106"    //Group id

    install {

        repositories.mavenInstaller {
            pom {
                project {
                    packaging 'aar' //AAR format

                    // Add your description here
                    name 'fingerprint-dialog-compat'  //Name of the repo
                    description = 'FingerprintDialog from Android 28 (P) back ported to Android 23 (M).'
                    url siteUrl

                    // Set your license
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'kevalpatel2106'     //Dev id
                            name 'Keval Patel'      //Dev name
                            email 'kevalpatel2106@gmail.com'    //Dev email
                        }
                    }
                    scm {
                        connection gitUrl
                        developerConnection gitUrl
                        url siteUrl
                    }
                }
            }
        }
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    // https://github.com/bintray/gradle-bintray-plugin
    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")

        configurations = ['archives']

        pkg {
            repo = "maven"

            // it is the name that appears in bintray when logged
            name =  'fingerprint-dialog-compat'  //Name of the repo
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            licenses = ["Apache-2.0"]
            publish = true
            version {
                gpg {
                    sign = false
                    //Determines whether to GPG sign the files. The default is false
                    //passphrase = properties.getProperty("bintray.gpg.password")   //Optional. The passphrase for GPG signing'
                }
            }
        }
    }
}
